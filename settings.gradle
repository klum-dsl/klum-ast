pluginManagement {
    plugins {
        id 'nebula.release' version '19.0.6'
        id "com.github.hierynomus.license" version "0.16.1"
        id "com.github.ben-manes.versions" version "0.51.0"
        id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
        id 'com.gradle.plugin-publish' version '1.2.1'
        id 'org.ajoberstar.git-publish' version '3.0.1'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }

    versionCatalogs {
        libs {
            version("groovy-v2", "2.4.21")
            version("groovy-v3", "3.0.21")
            version("groovy-v4", "4.0.21")
            library('groovy-v2', 'org.codehaus.groovy', 'groovy-all').versionRef("groovy-v2")
            library('groovy-v3', 'org.codehaus.groovy', 'groovy-all').versionRef("groovy-v3")
            library('groovy-v4', 'org.apache.groovy', 'groovy-all').versionRef("groovy-v4")

            library("jb-anno", "org.jetbrains:annotations:16.0.2")

            version("klum-cast", "0.1.0")
            library("klum-cast-compile", "com.blackbuild.klum.cast", "klum-cast-compile").versionRef("klum-cast")
            library("klum-cast-annotations", "com.blackbuild.klum.cast", "klum-cast-annotations").versionRef("klum-cast")

            version("spock-g2", "1.3-groovy-2.4")
            version("spock-g3", "2.3-groovy-3.0")
            version("spock-g4", "2.3-groovy-4.0")

            library("spock-g2", "org.spockframework", "spock-core").versionRef("spock-g2")

            library("spock-g3", "org.spockframework", "spock-core").versionRef("spock-g3")
            library("spock-junit4-g3", "org.spockframework", "spock-junit4").versionRef("spock-g3")

            library("spock-g4", "org.spockframework", "spock-core").versionRef("spock-g4")
            library("spock-junit4-g4", "org.spockframework", "spock-junit4").versionRef("spock-g4")

            bundle("spock-groovy-v2", ["spock-g2", "groovy-v2"])
            bundle("spock-groovy-v3", ["spock-g3", "spock-junit4-g3", "groovy-v3"])
            bundle("spock-groovy-v4", ["spock-g4", "spock-junit4-g4", "groovy-v4"])

            library "bytebuddy", "net.bytebuddy:byte-buddy:1.9.3"
            library "objenesis", "org.objenesis:objenesis:2.6"

            library "jpl", "org.junit.platform:junit-platform-launcher:1.9.0"

            bundle "spockRuntime", ["bytebuddy", "objenesis"]

            version("annodocimal", "0.1.0-rc.18")
            library("annodocimal-annotations", "com.blackbuild.annodocimal", "anno-docimal-annotations").versionRef("annodocimal")
            library("annodocimal-global-ast", "com.blackbuild.annodocimal", "anno-docimal-global-ast").versionRef("annodocimal")
        }
    }
}

rootProject.name = 'klum-ast-root'
include 'klum-ast-annotations'
include 'klum-ast'
include 'klum-ast-runtime'
include 'klum-ast-jackson'

// includeBuild "../anno-docimal"

