import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'nebula.release'
    id 'base'

    id "com.github.hierynomus.license"
    id 'org.ajoberstar.git-publish'
    id "com.github.ben-manes.versions"
    id "io.github.gradle-nexus.publish-plugin"
}

ext {
    groovyVersion = project.findProperty("groovyVersion") ?: 'v2'
    if (!(groovyVersion in ['v2', 'v3', 'v4']))
        throw new GradleException("Unsupported Groovy version $groovyVersion")
}

group 'com.blackbuild.klum.ast'
description 'A transformation for creating convenient configuration model DSLs.'

nexusPublishing {
    repositories {
        sonatype()
    }
}

gitPublish {
    repoUri = 'git@github.com:klum-dsl/klum-ast.wiki.git'
    branch = 'master'

    contents {
        from 'wiki'
        filesMatching('*.md') {
            filter(ReplaceTokens, tokens: [version: version.toString()])
        }
        from('.') {
            include("CHANGES.md")
            rename('CHANGES.md', "Changelog.md")
        }
    }

    repoDir = layout.buildDirectory.dir("gitPublish")
    
    commitMessage = "updated wiki for $version"
}

releaseCheck.doLast {
    if (!gradle.includedBuilds.isEmpty())
        throw new GradleException("Releasing is not allowed with composite builds. Please release $gradle.includedBuilds separately")

    if (rootProject.ext.groovyVersion != "v2")
        throw new GradleException("Releasing must be done with Groovy 2.4")
}

evaluationDependsOnChildren()

tasks.candidate.finalizedBy tasks.gitPublishPush
tasks.final.finalizedBy tasks.gitPublishPush

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
