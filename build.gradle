buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'net.researchgate:gradle-release:2.4.1'
    }
}

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'net.researchgate.release'


sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.7.8"
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "com.intellij:openapi:7.0.3"
    testCompile("org.spockframework:spock-core:${spockVersion}")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5.0'
}

release {
    tagTemplate = 'v${version}'
}

nexusStaging {
    packageGroup = "com.blackbuild"
}

modifyPom {
    project {
        name 'KlumDSL'
        packaging 'jar'
        description 'A transformation for creating convenient configuration model DSLs.'
        url 'https://github.com/klum-dsl/klum-ast'

        scm {
            connection 'scm:svn:https://github.com/klum-dsl/klum-ast.git'
            developerConnection 'scm:svn:https://github.com/klum-dsl/klum-ast.git'
            url 'https://github.com/klum-dsl/klum-ast'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'pauxus'
                name 'Stephan Pauxberger'
                email 'stephan@blackbuild.com'
            }
        }
    }
}

afterEvaluate {
    // modify dependecies for generated pom
    project.poms*.whenConfigured { pom ->
        pom.dependencies.find { it.artifactId == "groovy-all" }.optional = true
        pom.dependencies.find { it.artifactId == "openapi" }.optional = true
    }
}

afterReleaseBuild.dependsOn uploadArchives