import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'nebula.release'
    id 'base'

    id "com.github.hierynomus.license"
    id 'org.ajoberstar.git-publish'
    id "com.github.ben-manes.versions"
    id "io.github.gradle-nexus.publish-plugin"
    id "org.sonarqube"
}

group 'com.blackbuild.klum.ast'
description 'A transformation for creating convenient configuration model DSLs.'

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "klum-dsl_klum-ast"
        property "sonar.organization", "klum-dsl"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.gradle.skipCompile", "true"
    }
}

gitPublish {
    repoUri = 'git@github.com:klum-dsl/klum-ast.wiki.git'
    branch = 'master'

    contents {
        from 'wiki'
        filesMatching('*.md') {
            filter(ReplaceTokens, tokens: [version: version.toString()])
        }
        from('.') {
            include("CHANGES.md")
            rename('CHANGES.md', "Changelog.md")
        }
    }

    repoDir = layout.buildDirectory.dir("gitPublish")
    
    commitMessage = "updated wiki for $version"
}

releaseCheck.doLast {
    if (!gradle.includedBuilds.isEmpty())
        throw new GradleException("Releasing is not allowed with composite builds. Please release $gradle.includedBuilds separately")
}

evaluationDependsOnChildren()

tasks.candidate.finalizedBy tasks.gitPublishPush
tasks.final.finalizedBy tasks.gitPublishPush

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
