import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'nebula.release'
    id 'base'

    id "com.github.hierynomus.license"
    id 'org.ajoberstar.git-publish'
    id "com.github.ben-manes.versions"
    id "io.github.gradle-nexus.publish-plugin"
}

ext {
    groovyVersion = project.findProperty("groovyVersion") ?: '2.4.21'
    // groovyVersion = '4.0.12'
}

group 'com.blackbuild.klum.ast'
description 'A transformation for creating convenient configuration model DSLs.'

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.hierynomus.license'

    group rootProject.group

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withJavadocJar()
        withSourcesJar()
    }

    jacoco {
        toolVersion = "0.8.11"
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:16.0.2"
        compileOnly "org.codehaus.groovy:groovy-all:2.4.21"

        //compileOnly "com.intellij:openapi:7.0.3"

        if (rootProject.ext.groovyVersion.startsWith("2.")) {
            testImplementation "org.codehaus.groovy:groovy-all:$rootProject.ext.groovyVersion"
            testImplementation("org.spockframework:spock-core:1.3-groovy-2.4")
        } else if (rootProject.ext.groovyVersion.startsWith("3.")) {
            testImplementation "org.codehaus.groovy:groovy-all:$rootProject.ext.groovyVersion"
            testImplementation("org.spockframework:spock-core:2.3-groovy-3.0")
            testImplementation("org.spockframework:spock-junit4:2.3-groovy-3.0")
        } else if (rootProject.ext.groovyVersion.startsWith("4.")){
            testImplementation "org.apache.groovy:groovy-all:$rootProject.ext.groovyVersion"
            testImplementation("org.spockframework:spock-core:2.3-groovy-4.0")
            testImplementation("org.spockframework:spock-junit4:2.3-groovy-4.0")
        } else {
            throw new IllegalStateException("Unsupported Groovy version ${rootProject.ext.groovyVersion}")
        }
        testImplementation "org.jetbrains:annotations:16.0.2"

        testRuntimeOnly "net.bytebuddy:byte-buddy:1.9.3"
        testRuntimeOnly "org.objenesis:objenesis:2.6"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                suppressPomMetadataWarningsFor('testFixturesApiElements')
                suppressPomMetadataWarningsFor('testFixturesRuntimeElements')
                from(components.java)
                pom { pom ->
                    rootProject.configurePom(pom, project)
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") || gradle.taskGraph.hasTask("publishToMavenLocal") }
        sign publishing.publications.mavenJava
    }

    license {
        header = rootProject.file("LICENSE")
        mapping("java", "SLASHSTAR_STYLE")
        mapping("groovy", "SLASHSTAR_STYLE")
        mapping("gdsl", "SLASHSTAR_STYLE")
        exclude("mockup/**")
        strictCheck(true)
    }

    test {
        if (!rootProject.ext.groovyVersion.startsWith("2."))
            useJUnitPlatform()
    }

}

gitPublish {
    repoUri = 'git@github.com:klum-dsl/klum-ast.wiki.git'
    branch = 'master'

    contents {
        from 'wiki'
        filesMatching('*.md') {
            filter(ReplaceTokens, tokens: [version: version.toString()])
        }
        from('.') {
            include("CHANGES.md")
            rename('CHANGES.md', "Changelog.md")
        }
    }

    repoDir = file("$buildDir/gitPublish")
    
    commitMessage = "updated wiki for $version"
}

releaseCheck.doLast {
    if (!gradle.includedBuilds.isEmpty())
        throw new GradleException("Releasing is not allowed with composite builds. Please release $gradle.includedBuilds separately")

    if (JavaVersion.current() != JavaVersion.VERSION_11)
        throw new GradleException("Releasing must be done with JDK 11")

    if (!rootProject.ext.groovyVersion.startsWith("2."))
        throw new GradleException("Releasing must be done with Groovy 2.4")
}

evaluationDependsOnChildren()

tasks.candidate.finalizedBy tasks.gitPublishPush
tasks.final.finalizedBy tasks.gitPublishPush

def configurePom(pom, Project module) {
    pom.name = module.name
    pom.description = module.description

    pom.url = 'https://github.com/klum-dsl/klum-ast.git'

    pom.scm {
        url = 'https://github.com/klum-dsl/klum-ast.git'
        connection = 'scm:git:https://github.com/klum-dsl/klum-ast.git'
        developerConnection = 'scm:git:https://github.com/klum-dsl/klum-ast.git'
    }

    pom.licenses {
        license {
            name = 'MIT License (MIT)'
            url = 'https://opensource.org/licenses/MIT'
            distribution = 'repo'
        }
    }

    pom.developers {
        developer {
            id = 'pauxus'
            name = 'Stephan Pauxberger'
            email = 'stephan@blackbuild.com'
            url = 'https://github.com/pauxus'
        }
    }
}
